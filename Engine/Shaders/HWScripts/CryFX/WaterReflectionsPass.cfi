// Copyright 2001-2018 Crytek GmbH / Crytek Group. All rights reserved.

#include "Reflection.cfi"

// Adapted from DeferredShading.cfx SSRefl - todo: generalize for plane
half4 ReflectionPassPS( v2f_hs IN, half4 vNormal )
{
	// Do the specular reflection with ray marching.
	half4 specularRefl = half4(0,0,0,0);

	{
		const float3 vPositionWS = IN.vPosWS.xyz;
		const float2 ScreenTC = MapViewportToRaster(IN.baseTC.xy);
		const float2 halfTexel = 0;

		SPerPassWaterParams perPassParams = GetPerPassWaterParams();
		ReflParams params;
	
		params.ScreenTC = ScreenTC;
		params.ProjRatio = g_PS_ProjRatio.xy;
		params.JitterUV = IN.baseTC.zw;
		params.NearFarClipDist = CV_NearFarClipDist.xy;
		params.UnscaledMultiplier = float2(2.0f, 2.0f);
		params.ViewVec = normalize(vPositionWS - g_VS_WorldViewPos);
		params.fDepth = IN.screenProj.w * CV_NearFarClipDist.w;
		params.vPositionWS = vPositionWS;
		params.maxLum = 10000.0f;
		params.NormalWorld = vNormal.xyz;
		params.Smoothness = vNormal.w;
		params.Reproject = false;
		params.ViewProjPrev = 0;
	
		// local reflections setup
		{
			params.MaxDistance = perPassParams.ssrParams.z;
			params.MaxSamples = perPassParams.ssrParams.w;
			params.borderSize = float2(0.07, 0.05);

			half4 color = SSRRaytrace
			(
				sceneDepthScaledTex,

				refractionHalfResTex,
				refractionHalfResTex,
				ssMaterialBilinearClamp,

				refractionHalfResTex,
				ssMaterialTrilinearClamp,

				params
			);
		
			specularRefl = color;
		}
	
		// far reflection setup (if no or partially hit only)
		[branch] if (specularRefl.a < 1.0f)
		{
			params.MaxDistance = perPassParams.ssrParams.z;
			params.MaxSamples = 1;
			params.borderSize = float2(0.15, 0.07);

			half4 color = SSRaycast
			(
				sceneDepthScaledTex,

				refractionHalfResTex,
				refractionHalfResTex,
				ssMaterialBilinearClamp,

				refractionHalfResTex,
				ssMaterialTrilinearClamp,

				params
			);

			if (color.a >= specularRefl.a)
			{
				specularRefl.rgb = (specularRefl.rgb * specularRefl.a) + (color.rgb * (1.0f - specularRefl.a));
				specularRefl.a = color.a;
			}
			else
			{
				specularRefl.rgb = (color.rgb * color.a) + (specularRefl.rgb * (1.0f - color.a));
				specularRefl.a = specularRefl.a;
			}
		}
	}

	return specularRefl;
}

pixout WaterReflPS(v2f_hs IN)
{
	pixout OUT = (pixout) 1;

	const float fDepth = IN.screenProj.w * CV_NearFarClipDist.w;
	float2 linearUV = IN.screenProj.xy / IN.screenProj.w;
	float2 screenTC = MapViewportToRaster(linearUV);
	float fDepthCenter = sceneDepthScaledTex.Sample(ssPointClamp, screenTC);

	clip( fDepthCenter - fDepth );

	half3 vView = normalize(IN.vView.xyz);

#if 0
	// Generate normal map from 4 normal map layers + paralax
	float2 vParalaxOffset = GetParalaxOffset( IN, vView );  
	half  cSmoothness = (half)  1;
	half4 cDiffuseMap = (half4) 1;
	half3 vNormal = BumpGen( IN, vParalaxOffset, cSmoothness, cDiffuseMap);
#else
	half  cSmoothness = 1;
	half4 cDiffuseMap = (half4) 1;
	half3 vNormal = half3(0, 0, 1);
#endif

	half fNdotE =  (dot( vView.xyz, vNormal ) ) * IN.vView.w;

	//////////////////////////////////////////////////////////////////////////////////////////////////    
	// Get reflection color
	const half ReflectionBumpScale = 1.0;
	
	half3 vDir = g_VS_WorldViewPos - IN.vPosWS.xyz;
	half3 vReflN = lerp(half3(0,0,1), vNormal, ReflectionBumpScale);
	half3 vRefl = reflect(vDir, vReflN);
	half3 vReflNorm = normalize( vRefl );

#if %_RT_TILED_SHADING
	half3 cCubeRefl = GatherCubemapSpecular(screenTC, IN.vPosWS.xyz - GetWorldViewPos(), vNormal, cSmoothness);
#else
	half3 cCubeRefl = EnvCubeBoxProjected(IN.vPosWS.xyz, vReflNorm, cSmoothness);
#endif

	// Blend in screenspace reflections
	half4 cSSRefl = ReflectionPassPS(IN, half4(vReflN.xyz, 1)) / PS_HDR_RANGE_ADAPT_MAX;

	// EnvCubeReflMul, RealtimeReflMul
	half3 cReflCurr = lerp(cCubeRefl, cSSRefl, saturate(cSSRefl.a));

	// TODO: Remove fog from reflection
#if %_RT_FOG && %_RT_VOLUMETRIC_FOG
	//cReflCurr -= fogColor * ReflFogScale;
#endif

	//////////////////////////////////////////////////////////////////////////////////////////////////    
	// Continuously blend previous frame, minimize shimering
	float4 vPrevHSPos = HPosToScreenTC( mul(CV_PrevViewProjMatr, float4(IN.vPosWS.xyz, 1.0 )), false );
	half2 prevTC = vPrevHSPos.xy / vPrevHSPos.w;
	prevTC.x *= CV_HPosScale.z;
	prevTC.y = 1.0 - (1.0 - prevTC.y) * CV_HPosScale.w;

	prevTC = MapViewportToRaster(prevTC);

	half3 cReflPrev = reflectionPrevTex.Sample(ssMaterialTrilinearClamp, prevTC.xy).rgb / PS_HDR_RANGE_ADAPT_MAX;
	cReflPrev = isfinite( cReflPrev ) ? cReflPrev : 0;  // Filter out NANs that we still have sometimes, otherwise they get propagated and remain in the view

	OUT.Color.rgb = (cReflCurr * 0.75 + cReflPrev * 0.25) * PS_HDR_RANGE_ADAPT_MAX;

	return OUT;
}

//////////////////////////////// technique ////////////////

technique WaterReflPass
{
	pass p0
	{
		ZEnable = false; // todo use downscaled depth target for zculling
		ZFunc = LEqual;
		CullMode = None;
		ZWriteEnable = false;
		IgnoreMaterialState = true;

		VertexShader = WaterVS() WaterSurfaceVS;
		GeometryShader = $AutoGS_MultiRes();
#if %WATER_TESSELLATION_DX11
		HullShader   = WaterHS() WaterSurfaceHS;
		DomainShader = WaterReflDS() WaterSurfaceDS;
#endif

		PixelShader = WaterReflPS() WaterSurfacePS;
	}
}
